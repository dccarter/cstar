pub extern func strlen(s: string) : u64
pub extern func sprintf(s: &char, fmt: string, ...args: auto) : i64;

pub extern func memmove(dst: &void, src: &const void, num: u64) : &void;
pub extern func malloc(s: u64): &void
pub extern func realloc(ptr: &void, s: u64): &void
pub extern func free(ptr: &void): void
pub extern func abort(): void;

pub extern func write(fd: i32, buf: &const void, count: u64) : i64

@[pure, inline]
pub func __cxy_alloc(size: u64) => malloc(size)
@[pure, inline]
pub func __cxy_realloc(ptr: &void, size: u64) => realloc(ptr, size)
@[pure, inline]
pub func __cxy_free(ptr: &void) { free(ptr) }

@[pure, inline]
pub func __cxy_assert(cond: bool, file: string, line: u64, column: u64) {
    if (!cond) {
        var buf: [char, 64];
        write(2, "assertion failed: ", 18)
        write(2, file, strlen(file))
        var count = sprintf(buf, ":%u:%u\n", line, column);
        write(2, buf, count)
        abort()
    }
}

/* Builtin Optional Type */
struct __Optional[T] {
    /* @note: Do not change the order of these members */
    - ok: bool = false;
    - val: T

    @inline func `init`() { ok = false }
    @inline func `init`(value: T) { val = value; ok = true }

    @inline func `!!`() => ok
    @inline func `deref`() { return val }
}

/* Creates a valid optional value */
@inline
pub func Some[T](@transient value: T) => __Optional[T](value)

/* Creates an invalid optional value */
@inline
pub func None[T]() => __Optional[T]()

pub type Hash = u32
#const FNV_32_PRIME = 0x01000193

@inline
pub func hash_fnv1a_uint8(h: Hash, x: u8) : Hash
{
    return (h ^ x) * #{FNV_32_PRIME}
}

pub func hash_fnv1a_uint16(h: Hash, x: u16) =>
    hash_fnv1a_uint8(hash_fnv1a_uint8(h, <u8>(x & 0xff)), <u8>(x >> 8))

pub func hash_fnv1a_uint32(h: Hash, x: u32) =>
    hash_fnv1a_uint16(hash_fnv1a_uint16(h, <u16>(x & 0xffff)), <u16>(x >> 16))

pub func hash_fnv1a_uint64(h: Hash, x: u64) =>
    hash_fnv1a_uint32(hash_fnv1a_uint32(h, <u32>(x & 0xffffffff)), <u32>(x >> 32))

pub func hash_fnv1a_ptr(h: Hash, ptr: &const void) =>
    hash_fnv1a_uint64(h, ptr !: u64)

pub func hash_fnv1a_string(h: Hash, str: string)
{
    var i = 0;
    while (str.[i] != '\0') {
        h = hash_fnv1a_uint8(h, <u8>str.[i]);
        i++
    }
    return h;
}

pub func hash_fnv1a_bytes(h: Hash,
                         ptr: &const void,
                         size: u64)
{
    const p = ptr: &const u8;
    for (const i: 0..size-1)
        h = hash_fnv1a_uint8(h, p.[i]);
    return h
}

pub func hash[T](val: const T, init: Hash = #{FNV_32_PRIME}) : Hash {
    #if (#T == #string) {
        return hash_fnv1a_string(init, val)
    }
    else #if (#T == #i8 || #T == #u8) {
        return hash_fnv1a_uint8(init, <u8>val)
    }
    else #if (#T == #i16 || #T == #u16) {
        return hash_fnv1a_uint16(init, <u16>val)
    }
    else #if (#T == #i32 || #T == #u32) {
        return hash_fnv1a_uint32(init, <u32>val)
    }
    else #if (T.isFloat || #T == #i64 || #T == #u64) {
        return hash_fnv1a_uint64(init, <u64>val)
    }
    else #if (T.isEnum) {
        return hash_fnv1a_uint64(init, <u64>val)
    }
    else #if (T.isTuple) {
        var code = #{FNV_32_PRIME};
        #for (const i: 0..T.membersCount) {
            code = hash(val.#{i}, code)
        }
        return code
    }
    else #if (T.isArray || T.isSlice) {
        var code = #{FNV_32_PRIME};
        for (const x: val) {
            code = hash(x, code)
        }
        return code
    }
    else #if (T.isUnion) {
        var code = #{FNV_32_PRIME};
        match (val) {
            #for (const i: 0..#{T.membersCount}) {
                case #{typeat!(T, i)} as y => {
                    code = hash(#{i}, code)
                    code = hash(y, code)
                }
            }
        }
        return code;
    }
    else {
        // this will fail if T does not implement hash function
        return val.op__hash()
    }
}

#const CXY_STRING_BUILDER_DEFAULT_CAPACITY = 32:u64

class String {
    - capacity: u64 = 0;
    - count: u64 = 0;
    - data: &char = null;

    - func grow(growSize: u64) {
        const newSize = count + growSize;
        if (this.data == null) {
            this.data = <&char> __cxy_alloc(growSize + 1)
            this.capacity = growSize
        }
        else if (this.capacity < newSize) {
            while (this.capacity < newSize) {
                this.capacity <<= 1
            }
            this.data = <&char> __cxy_realloc(this.data !: &void, this.capacity+1)
        }
    }

    @inline
    func `init`() {}

    @inline
    func `init`(str: string) {
        appendString(str)
    }

    @inline
    func `deinit`() => {
        //if (data != null) {
            __cxy_free(this.data)
            this.data = null
        //}
    }

    func appendString(str: &const char, len: u64) {
        if (len) {
            grow(len)
            var p = this.data + this.count;
            memmove(p, str, len)
            this.count += len
            this.data.[this.count] = <char>'\0'
        }
    }

    @inline
    func appendString(s: string) : void {
        appendString(s: &const char, strlen(s))
    }

    func appendSignedInt(num: i64) {
        var buf: [char, 64];
        const len = sprintf(buf, "%lld", num);
        appendString(buf, len)
    }

    func appendUnSignedInt(num: u64) {
        var buf: [char, 32];
        const len = sprintf(buf, "%llu", num);
        appendString(buf, len)
    }

    func appendFloat(num: f64) {
        var data: [char, 32];
        const len = sprintf(data, "%g", num);
        appendString(data, len)
    }

    func appendChar(ch: wchar) {
        if (ch < 0x80) {
            grow(1);
            this.data.[this.count++] = <char> ch
        }
        else if (ch < 0x800) {
            grow(2);
            this.data.[this.count++] = <char> (0xC0 | (ch >> 6))
            this.data.[this.count++] = <char> (0x80 | (ch & 0x3f))
        }
        else if (ch < 0x10000) {
            grow(3);
            this.data.[this.count++] = <char> (0xE0 | (ch >> 12))
            this.data.[this.count++] = <char> (0x80 | ((ch >> 6) & 0x3f))
            this.data.[this.count++] = <char> (0x80 | (ch & 0x3f))
        }
        else if (ch < 0x200000) {
            grow(4);
            this.data.[this.count++] = <char> (0xF0 | (ch >> 16))
            this.data.[this.count++] = <char> (0x80 | ((ch >> 12) & 0x3f))
            this.data.[this.count++] = <char> (0x80 | ((ch >> 6) & 0x3f))
            this.data.[this.count++] = <char> (0x80 | (ch & 0x3f))
        }
        else {
            // TODO raise OutOfRangeException()
        }
        this.data.[this.count] = <char>'\0'
    }


    @inline
    func appendBool(val: bool) {
        if (val)
            appendString("true": &const char, 4)
        else
            appendString("false": &const char, 5)
    }

    func appendPointer[T](ptr: &const T) {
        var data: [char, 32];
        const len = sprintf(data, "%p", ptr: &const void);
        appendString(data, len)
    }

    @inline
    func `<<`[U](val: const U) : String {
        #if (U.isString)
            appendString(val)
        else #if (U.isInteger)
            appendSignedInt(<i64>val)
        else #if (U.isFloat)
            appendFloat(val)
        else #if (U.isBoolean)
            appendBool(val)
        else #if (#U == #wchar || #U == #char)
             appendChar(val)
        else #if (U.isEnum)
            appendString(val.__name)
        else #if (U.isStruct || U.isClass)
            val.op__str(this)
        else #if (U.isTuple) {
            this.appendChar('(')
            #for (const i: 0..U.membersCount) {
                #if (i != 0)
                    this.appendString(", ")
                this << val.#{i}
            }
            this.appendChar(')')
        }
        else #if (U.isArray || U.isSlice) {
            this.appendChar('[')
            for (const i: 0..len!(val)) {
                if (i != 0)
                    this.appendString(", ")
                this << val.[i]
            }
            this.appendChar(']')
        }
        else #if (U.isUnion) {
            match (val) {
                #for (const i: 0..#{U.membersCount}) {
                    case #{typeat!(U, i)} as y => this << y
                }
            }
        }
        return this
    }

    @inline
    func `+`[U](other: const U) => this << other

    @inline
    func `str`(sb: String) {
        sb.appendString(data, count)
    }

    @inline
    const func `hash`() {
        if (this.data) {
            return hash_fnv1a_bytes(#{FNV_32_PRIME}, this.data, this.count)
        }
        else {
            return #{FNV_32_PRIME}
        }
    }

    const func compare(other: const String) {
        if (count != other.count)
            return <i32>(count - other.count)

        for (const i : 0..count) {
            if (data.[i] != other.data.[i])
                return (<i32>data.[i]) - (<i32>other.data.[i])
        }

        return <i32>0
    }

    @inline
    const func `==`(other: const String) => compare(other) == 0

    @inline
    const func `!=`(other: const String) => compare(other) != 0

    @inline
    const func `<`(other: const String) => compare(other) < 0

    @inline
    const func `>`(other: const String) => compare(other) > 0

    @inline
    const func `<=`(other: const String) => compare(other) <= 0

    @inline
    const func `>=`(other: const String) => compare(other) >= 0

    @inline
    const func `[]`(idx: u32) {
        __cxy_assert(idx < count, file!, line!, column!)
        return this.data.[idx]
    }

    const func `..`() {
        var i:i32 = 0;
        return () : __Optional[(char, i32)] => {
            if (i < count)
                return Some((data.[i], i++))
            else
                return None[(char, i32)]()
        }
    }
}

pub struct Slice[T] {
    - data: &T
    - count: u64

    func `init`(data: &T, count: u64) {
        this.data = data
        this.count = count
    }

    @inline
    func `[]=`(index:  i64, data: T) {
        __cxy_assert(index < count, file!, line!, column!)
        this.data.[index] = data
    }

    @inline
    func `[]`(index:  i64) {
        __cxy_assert(index < count, file!, line!, column!)
        return this.data.[index]
    }

    @inline
    const func `[]`(index:  i64) {
        __cxy_assert(index < count, file!, line!, column!)
        return this.data.[index]
    }

    const func `..`() {
        var i: i32 = 0;
        return () : __Optional[(T, i32)] => {
            if (i < count)
                return Some((data.[i], i++))
            return None[(T, i32)]()
        }
    }

    const func `hash`() {
        var code: u32 = #{FNV_32_PRIME};
        for (const i: 0..count) {
            code = hash(data.[i], code)
        }
        return code
    }

    const func `str`(sb: String) {
        sb.appendChar('[')
        for (const i: 0..count) {
            if (i != 0)
                sb.appendString(", ")
            sb << data.[i]
        }
        sb.appendChar(']')
    }
}

@inline
pub func allocate[T](count: u32 = 0) => __cxy_alloc(sizeof!(#T)) : T

@inline
pub func deallocate[T](ptr: T) {
    #if (T.has_deinit) {
        ptr.op__deinit()
    }
    __cxy_free(ptr: &void)
}

func print[T](data: const T) {
    #if (T.isString) {
        #if (T.isClass) {
            write(0, data.data, data.count)
        }
        else {
            write(0, data, strlen(data))
        }
    }
    else {
        error!("'{t}' currently not supported", #T)
    }
}

func println(...args: auto) {
    var s = String();
    #for (const x: args) {
        s << #{x}
    }
    write(0, s.data, s.count)
    write(0, "\n", 1)

    deallocate(s)
}
