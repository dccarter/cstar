module logger

import { Time } from "./time.cxy"

pub enum Level {
    @str("TRC")
    TRACE,
    @str("DBG")
    DEBUG,
    @str("INF")
    INFO,
    @str("WRN")
    WARNING,
    @str("ERR")
    ERROR
}

struct FixedWidth {
    - s: __string
    - count: u64
    func `init`(s: __string, count: u64) {
        this.s = s
        this.count = count
    }

    @inline
    const func `str`(os: &OutputStream) {
        os << s.substr(0 as u64, <i64>min(count, s.size()))
        if (s.size() < count)
            os << Repeat( ' ', count - s.size() )
    }
}

#if (!defined CXY_LOG_LEVEL) {
    macro CXY_LOG_LEVEL = .TRACE
}

class Logger {
    - os: OutputStream;
    - _level: Level = CXY_LOG_LEVEL!;

    func `init`() {
        os = stdout
    }

    func setOutputStream(os: OutputStream) {
        this.os = &&os
    }

    func level(lvl: Level) {
         _level = lvl
    }

    const func level() => _level

    func log(lvl: Level, tag: string) : &OutputStream {
        Time().format(LOG_TIME_FMT!, &os)
        os << " " << lvl << " " << FixedWidth(tag, 10) << " "
        return &os
    }
}

pub var __L = Logger();

macro __LOG(LVL, MSG) { if (LVL! >= logger.__L.level()) lshift!(logger.__L.log(LVL!, LOG_TAG), MSG!) << '\n' }
macro TRC(MSG) =( __LOG!(logger.Level.TRACE, MSG!) )
macro DBG(MSG) =( __LOG!(logger.Level.DEBUG, MSG!) )
macro INF(MSG) =( __LOG!(logger.Level.INFO, MSG!) )
macro WRN(MSG) =( __LOG!(logger.Level.WARNING, MSG!) )
macro ERR(MSG) =( __LOG!(logger.Level.ERROR, MSG!) )

#if (defined CXY_TRACE_LEVEL) {
    #if (CXY_TRACE_LEVEL! == 3) {
        macro TRC3(MSG) TRC!(MSG!)
        macro TRC2(MSG) TRC!(MSG!)
        macro TRC1(MSG) TRC!(MSG!)
    }
    else #if (CXY_TRACE_LEVEL! == 2) {
        macro TRC3(MSG) ()
        macro TRC2(MSG) TRC!(MSG!)
        macro TRC1(MSG) TRC!(MSG!)
    }
    else #if (CXY_TRACE_LEVEL! == 1) {
        macro TRC3(MSG) ()
        macro TRC2(MSG) ()
        macro TRC1(MSG) TRC!(MSG!)
    }
}
else {
    macro TRC3(MSG) ()
    macro TRC2(MSG) ()
    macro TRC1(MSG) ()
}

@inline
pub func setLogLevel(lvl: Level) {
    __L.level(lvl)
}
