module tcp

import { Address, Socket } from "./net.cxy"
import { State } from "./coro.cxy"

import "sys/socket.h" as socket
import "sys/errno.h" as errno
import "unistd.h" as unistd
##if (defined MACOS) {
    import "sys/fcntl.h" as fcntl
}
else {
    import "fcntl.h" as fcntl
    import "bits/socket_type.h" as _st
    import "sys/sendfile.h" as sf
}

#if (!defined MACOS) {
    macro SOCK_STREAM _st.__socket_type.SOCK_STREAM

    @inline
    func Socket_sendfile(outFd: i32, inFd: i32, off: ^u64, count: u64) {
        var offset = <i64>*off;
        var status = sf.sendfile(outFd, inFd, ptrof offset, count)
        if (status == 0)
            *off = offset
        return status
    }
}
else {
    @inline
    func Socket_sendfile(outFd: i32, inFd: i32, off: ^u64, count: u64) {
        var size = <i64> count;
        var status = socket.sendfile(inFd, outFd, <i64>*off, ptrof size, null, 0 as i32)
        if (status == 0)
            *off = *off + size
        return status
    }
}

func configureSocket(fd: i32)  {
    /* Make the socket non-blocking. */
    var opt = fcntl.fcntl(fd, F_GETFL!, 0);
    if (opt == -1)
        opt = 0;
    @unused var rc = fcntl.fcntl(fd, F_SETFL!, opt | O_NONBLOCK!);
    assert!(rc != -1);
    /*  Allow re-using the same local address rapidly. */
    opt = 1;
    rc = socket.setsockopt(fd, SOL_SOCKET!, SO_REUSEADDR!, ptrof opt, <u32>sizeof!(opt));
    assert!(rc == 0);
}

pub class TcpSocket: Socket {
    func `init`(fd: i32, addr: Address) {
        super(fd, &&addr)
    }

    func `init`() {
        super(-1 as i32, Address())
    }

    func receive(buffer: ^void, size: u64, timeout: u64 = 0): u64? {
        var remaining = size;
        var data = buffer !: ^char;
        var total: u64 = 0;

        while (super._fd != -1 && remaining > 0) {
            var sz = socket.recv(super._fd, ptrof data.[total], remaining, 0);
            if (sz == 0) {
                if (total > 0)
                    return total

                close()
                return null
            }

            if (sz == -1 || sz == 0) {
                if (errno! != EAGAIN! && errno! != EWOULDBLOCK!) {
                    if (errno! == ECONNRESET!)
                        close()
                    return null
                }

                if (total > 0)
                    return total

                if (!wait(timeout, State.AE_READABLE))
                    return null

                continue
            }

            total += sz
            remaining -= sz
        }

        return total
    }

    func sendBuffer(buffer: ^const void, size: u64, timeout: u64 = 0): u64? {
        var data = buffer !: ^const char;
        var remaining = size;
        var total: u64 = 0;
        while (super._fd != -1 && remaining > 0) {
            var sz = socket.send(super._fd, ptrof data.[total], remaining, 0);
            if (sz == -1 || sz == 0) {
                if(errno! == EPIPE!) {
                    errno! = ECONNRESET!
                    close()
                    return null
                }

                if (errno! != EAGAIN! && errno! != EWOULDBLOCK!)
                    return null

                if (!wait(timeout, State.AE_WRITABLE))
                    return null

                continue
            }

            total += sz
            remaining -= sz
        }
        errno! = 0
        return total
    }

    func sendFile(fd: i32, offset: u64, count: u64, timeout: u64 = 0): u64? {
        var toffset = offset + count
        var remaining = count
        while (toffset > offset) {
            var sz = Socket_sendfile(super._fd, fd, ptrof offset, remaining)
            if (sz == -1 || sz == 0) {
                if (errno!  == EPIPE!) {
                    errno! = ECONNRESET!
                    close()
                    return null
                }

                if (errno! != EAGAIN! && errno! != EWOULDBLOCK!)
                    return null

                if (!wait(timeout, State.AE_WRITABLE))
                    return null
                continue
            }
            remaining -= sz
        }
        errno! = 0
        return count
    }
}

pub class TcpListener {
    - bind: Address
    - fd: i32

    func `init`(addr: Address) {
        bind = addr
        fd = -1
    }

    func `!!`() => (this !: ^void) != null && fd != -1

    func listen(backlog: i32 = 127) {
        fd = socket.socket(bind.family(), <i32>SOCK_STREAM!, 0)
        if (fd == -1) {
            return false
        }
        configureSocket(fd)

        var rc = socket.bind(fd, bind.nativeAddr(), <u32>bind.len());
        if (rc == -1) {
            close()
            return false
        }

        rc = socket.listen(fd, backlog)
        if (rc != 0) {
            close()
            return false
        }

        if (bind.port() == 0) {
            var len = <u32> sizeof!(bind);
            rc = socket.getsockname(fd, bind.nativeAddr(), ptrof len)
            if (rc == -1) {
                close()
                return false
            }
        }

        return true
    }

    func accept(timeout: u64 = 0): TcpSocket? {
        var addr = Address();
        while (this) {
            /* Try to get new connection (non-blocking). */
            var len = <u32> sizeof!(addr);
            var accepted = socket.accept(fd, addr.nativeAddr(), ptrof len);
            if (accepted >= 0) {
                configureSocket(this.fd)
                return TcpSocket(accepted, addr)
            }

            if (errno! != EAGAIN! && errno! != EWOULDBLOCK!)
                return null
            /* Wait till new connection is available. */
            var rc = fdWaitRead(fd, timeout);
            if (rc != State.AE_READABLE) {
                errno! = ETIMEDOUT!;
                return null
            }
        }
        return null
    }

    @inline
    func close() : void { shutdown() }

    func shutdown(how: i32 = <i32>SHUT_RDWR!): void {
        if (fd != -1) {
            socket.shutdown(fd, how)
            unistd.close(fd)
            fd = -1
        }
    }

    func `deinit`() { close(); }
}

exception TcpError(msg: String) => msg == null? "": msg.str()

pub func tcpConnect(addr: Address, timeout: u64 = 0): !i32 {
    /* Open a socket. */
    var fd = socket.socket(addr.family(), <i32>SOCK_STREAM!, 0);
    if (fd == -1)
        raise TcpError(f"socket.socket(${addr}) failed: ${strerr()}")

    /* Connect to the remote endpoint. */
    var rc = socket.connect(fd, addr.nativeAddr(), <i32>addr.len());
    if (rc != 0) {
        assert!(rc == -1)
        if (errno! != EINPROGRESS!)
            raise TcpError(f"socket.connect(${addr}) failed: ${strerr()}")

        rc = fdWaitWrite(fd, timeout)
        if (rc != State.AE_WRITABLE) {
            errno! = ETIMEDOUT!
            raise TcpError(f"Waiting for socket(${addr}) to be writable failed")
        }

        var err: i32 = 0;
        var errSize = <u32>sizeof!(err);
        rc = socket.getsockopt(fd, SOL_SOCKET!, SO_ERROR!, (ptrof err) !: ^void, ptrof errSize)
        if (rc != 0) {
            err = errno!
            unistd.close(fd)
            errno! = err
            raise TcpError(f"socket.getsockopt(${addr}) failed: ${strerr()}")
        }

        if (err != 0) {
            unistd.close(fd)
            errno! = err
            raise TcpError(f"socket(${addr}) error: ${strerr()}")
        }
    }

    errno! = 0
    configureSocket(fd)
    return fd
}
