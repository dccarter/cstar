module json

pub func toJson[T](sb: &StringBuilder, it: &const T)
{
    #const Tinfo = #T;

    #if (Tinfo.isNumber || Tinfo.isBoolean || Tinfo.isChar) {
        sb << *it;
    }
    else #if (Tinfo.isString) {
        sb << '"' << *it << '"'
    }
    else #if (Tinfo.isOptional) {
        #const U = typeof!(**it);
        #if (U.isPointer) {
            toJson[#{U.pointedType}](sb, *it)
        }
        else {
            toJson[#{U}](sb, &it.val)
        }
    }
    else #if (Tinfo.isStruct) {
        sb << "{";
        #const first = true;
        #for (const member: Tinfo.members) {
            #if (first == false) {
                sb << ", ";
            }

            #if (member.isField) {
                sb << '"' << #{member.name} << "\": ";
                #if (member.isPointer)
                    toJson[#{member.Tinfo.pointedType}](sb, it.#{mkIdent!(member.name)})
                else
                    toJson[#{member.Tinfo}](sb, &it.#{mkIdent!(member.name)})
                #{first = false}
            }
        }
        sb << "}";
    }
    else #if (Tinfo.isArray) {
        sb << '[';
        var first = true;
        for (const member: *it) {
            if (!first) sb << ", "
            #if (Tinfo.elementType.isPointer)
                toJson[#{T.elementType.pointedType}](sb, member)
            else
                toJson[#{T.elementType}](sb, &member)

            first = false
        }
        sb << ']'
    }
}
