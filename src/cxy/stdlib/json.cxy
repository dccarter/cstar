module json

pub func toJson[T](@transient sb: StringBuilder, it: const T) : void {
    #if (T.isNumber || T.isBoolean || T.isChar) {
        sb << it
    }
    else #if (T.isString) {
        sb << '"' << it << '"'
    }
    else #if (T.isOptional) {
        if (it) {
            const value = *it;
            toJson[#{typeof!(value)}](sb, value)
        }
    }
    else #if (T.isStruct) {
        sb << "{";
        #const first = true;
        #for (const member: T.members) {
            #if (member.isField) {
                #if (first == false) {
                    sb << ", "
                }

                sb << '"' << #{member.name} << "\": ";
                toJson[#{member.Tinfo}](sb, it.#{mkIdent!(member.name)})
                #{first = false}
            }
        }
        sb << "}"
    }
    else #if (T.isTuple) {
        sb << "["
        @consistent
        #for (const i: 0..T.membersCount) {
            #if (i != 0) {
                sb << ", ";
            }

            var member = it.#{i};
            toJson(sb, member)
        }
        sb << "]"
    }
    else #if (T.isSlice) {
        sb << '[';
        var first = true;
        for (const member, _: it) {
            if (!first) sb << ", "

            toJson[#{T.elementType}](sb, member)

            first = false
        }
        sb << ']'
    }
    else {
        // give up here
        error!("type `{t}` is not a JSON-able type", T)
    }
}
