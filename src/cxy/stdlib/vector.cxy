module vector

const DEFAULT_VECTOR_CAPACITY : u64 = 16;

pub class Vector[T] {
    - data: &T
    - count: u64
    - capacity: u64

    - func grow() {
        if (count >= capacity) {
            capacity += (capacity / 2)
            data = <&T> CXY__realloc(data: &void, sizeof!(#T) * capacity, null)
        }
    }

    func `init`(initialSize: u64 = DEFAULT_VECTOR_CAPACITY) {
        capacity = initialSize
        data = <&T> CXY__alloc(sizeof!(#T) * capacity, null)
    }

    @inline
    func `deinit`() {
        #if (T.isDestructible) {
            for (const i: 0..count) {
                data.[i] = null
            }
        }
        CXY__free(data: &void)
    }

    func push(item: T) {
        grow()
        data.[count++] = item
    }

    func pop() {
        assert!(count > 0)
        count--
        return data.[count]
    }

    func shift() {
        assert!(count > 0)
        var item = data.[0];
        var tmp = data;
        count--
        memmove(tmp, ptroff!(tmp + 1), sizeof!(#T) * count)
        return item
    }

    func resize(newSize: u64) {
        data = <&T>CXY__realloc(data: &void, sizeof!(#T) * newSize, null)
        if (newSize < count)
            count = newSize
       capacity = newSize;
    }

    @inline
    func shrink() => resize(count)

    func `[]`(index: i32) {
        assert!(index < count)
        return data.[index]
    }

    func `[]=`(index: i32, value: T) {
        assert!(index < count)
        data.[index] = value
    }

    func `..`() {
        var i : i64 = 0;
        return () : (T, i64)? => {
            if (i < count)
                return (data.[i], i++)
            else
                return null
        }
    }

    func each(cb: func(item: T) -> void) {
        for (var i: 0..count) {
            cb(data.[i])
        }
    }

    const func `str`(sb: StringBuilder) {
        sb << '['
        for (var i: 0..count) {
            if (i != 0)
                sb << ", "
            sb << data.[i]
        }
        sb << ']'
    }
}
